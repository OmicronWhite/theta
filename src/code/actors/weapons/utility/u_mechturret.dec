// Anti-Mech turret
// This turret shot a powerfull anti-mech shot

actor Utility_MechTurretRed : Utility_TurretBase {
	obituary "%o was owned by a \cgRed \c-anti-Mech turret."
	health 500
	painchance 50
	//seesound "turret/sight"
	attacksound "rifle/fire"
	activesound "turret/sight"
	DesignatedTeam 1
	decal "VoltageScorch"
	states {

	Spawn:
		MECH A 0 Thing_SetSpecial(0, 84, 345, 0, TEAM_RED)
	Idle:
		MECH A 2 A_Look
		goto Idle
	See:
		MECH A 0 A_GiveInventory("IsRed", 1)
		MECH A 0 A_ChangeFlag("FRIENDLY", 0)
		MECH A 2 A_JumpIfTargetInLOS("Missile")
		MECH A 0 A_ClearTarget
		goto Idle
	Clear:
		MECH A 2 A_ClearTarget
		goto See
	Missile:
		MECH A 0 A_JumpIfInTargetInventory ("IsBlue", 1, 1)
		goto Clear
		MECH B 0 A_JumpIfInTargetInventory("IsMech", 1, 1)
		goto Clear
		MECH A 0 A_JumpIfCloser(3840,"MissileNormal")
		goto Clear
	MissileNormal:
		MECH B 4 A_FaceTarget //Turret shot delay
		MECH B 0 A_FaceTarget
		MECH B 0 A_SpawnItemEx("ShellCasing", 8, 8, 48, random(1,3), random(0,1), random(1,3), random(45,60), 128)
		MECH C 2 BRIGHT A_CustomBulletAttack(0, 0, 1 , 25, "AntimechPuff", 3840)
		MECH B 2
		MECH A 12
		MECH A 0 A_CPosRefire
		MECH A 0 A_JumpIfCloser(3840, 1) // Check if player is on max range
		goto MissileNormal+1
		MECH A 4 // Delay when out of range before choose another target
		goto Clear
	Pain:
		MECH A 2 A_ClearTarget
		goto See
	}
}

actor Utility_MechTurretBlue : Utility_MechTurretRed
{
	obituary "%o was owned by a \chBlue \c-anti-Mech turret."
	DesignatedTeam 0
	states {

	Spawn:
		MECH A 0
		MECH A 0 Thing_SetSpecial (0, 84, 345, 0, TEAM_BLUE)
	See:
		MECH A 0 A_GiveInventory("IsBlue", 1)
		MECH A 0 A_ChangeFlag("FRIENDLY", 0)
		MECH A 2 A_JumpIfTargetInLOS("Missile")
		MECH A 0 A_ClearTarget
		goto Idle
	Missile:
		MECH A 0 A_JumpIfInTargetInventory ("IsRed", 1, 1)
		goto Clear
		MECH B 0 A_JumpIfInTargetInventory("IsMech", 1, 1)
		goto Clear
		MECH A 0 A_JumpIfCloser(3840,"MissileNormal")
		goto Clear
	}
}

actor AntimechPuff : BulletPuff {
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOGRAVITY
	+FORCEXYBILLBOARD
	-ALLOWPARTICLES
	renderstyle Add
	alpha 0.5
	Scale 0.25
	DamageType "ExplosionMech"
	states {
	Spawn:
		VOLX ABCDEF 2 bright
	Meele:
		TNT0 A 0
		stop
	}
}