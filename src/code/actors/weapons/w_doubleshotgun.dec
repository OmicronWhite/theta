// Layout 2 applyed
// Double-barreled Shotgun
// Bullet - Close combat - No dependency - Price:200

actor DoubleShotgun : AOWWeapon {
	Inventory.Pickupmessage "You got the double shotgun!"
	Obituary "$OB_DOUBLESHOTGUN"
	Dropitem "DoubleShotgunPickup"
	// Sound defs
	Attacksound "weapons/sshotf"
	// Weapon defs
	Weapon.Selectionorder 16
	Weapon.Kickback 30
	Weapon.Ammotype "ShellsAmmo"
	Weapon.Ammouse 2
	Weapon.Ammogive 16
	Weapon.Preferredskin "MarineSSG"

	States {
	Deselect:
		SHT2 A 1 A_Lower
		loop

	Select:
		SHT2 A 1 A_Raise
		loop

	Ready:
		SHT2 A 1 A_WeaponReady
		loop

	Fire:
		SHT2 A 0 A_JumpIfInventory ("Classdoubleshotgun", 1, "Special") // Shot a more ranged attack if you are Double Shotgunner.
		SHT2 A 0 Bright A_GunFlash
		SHT2 A 7 Bright A_FireBullets (15.7, 11.7, 20, 8, "HandgunPuff", FBF_USEAMMO|FBF_NORANDOM, 512)
		SHT2 B 7
		SHT2 C 7 A_CheckReload
		SHT2 DD 0 A_SpawnItemEx ("ShotgunShellCasing", 0,0,32, random(1,3),random(0,1),0, random(45,60))
		SHT2 D 7 A_OpenShotgun2
		SHT2 E 7
		SHT2 F 7 A_LoadShotgun2
		SHT2 G 6
		SHT2 H 9 A_CloseShotgun2
		SHT2 A 5 A_ReFire
		goto Ready

	Special:
		SHT2 A 0 Bright A_GunFlash
		SHT2 A 7 Bright A_FireBullets (15.7, 11.7, 20, 8, "HandgunPuff", FBF_USEAMMO|FBF_NORANDOM, 768)
		SHT2 B 7
		SHT2 C 7 A_CheckReload
		SHT2 DD 0 A_SpawnItemEx ("ShotgunShellCasing", 0,0,32, random(1,3),random(0,1),0, random(45,60))
		SHT2 D 7 A_OpenShotgun2
		SHT2 E 7
		SHT2 F 7 A_LoadShotgun2
		SHT2 G 6
		SHT2 H 9 A_CloseShotgun2
		SHT2 A 5 A_ReFire
		goto Ready

	Flash:
		SHT2 I 3 Bright A_Light1
		SHT2 J 4 Bright A_Light2
		TNT1 A 28 A_Light0
		stop

	Spawn:
		SGN2 A -1
		stop
	}
}

// Map Actor

actor DoubleShotgunPickup : CustomInventory replaces SuperShotgun 21086 {
	//$Category Theta Weapons
	inventory.pickupmessage "You got the double shotgun!"
	inventory.maxamount 0
	inventory.respawntics 2100
	inventory.pickupsound "misc/w_pkup"
	states {

	Spawn:
		SGN2 A -1
		stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory ("DoubleShotgun", 1, "Failure")
		TNT1 A 0 A_GiveInventory("DoubleShotgun", 1)
		TNT1 A 0 A_GiveInventory("ShellsAmmo", 16)
		TNT1 A 0 A_GiveInventory("Refunds", 66)
		stop
	Failure:
		TNT1 A 0
		fail
	}
}
