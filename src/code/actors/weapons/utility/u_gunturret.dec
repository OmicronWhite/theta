// Gun turret
// Behaves exactly like a sub-machinegun

actor Utility_GunTurretRed : Utility_TurretBase {
	obituary "%o was owned by a \cgRed \c-turret."
	health 250
	painchance 50
	//seesound "turret/sight"
	attacksound "subby/fire"
	activesound "turret/sight"
	DesignatedTeam 1
	states {

	Spawn:
		GUNT A 0 Thing_SetSpecial(0, 84, 345, 0, TEAM_RED)
	Idle:
		GUNT A 2 A_Look
		goto Idle
	See:
		GUNT A 0 A_GiveInventory("IsRed", 1)
		GUNT A 0 A_ChangeFlag("FRIENDLY", 0)
		GUNT A 2 A_JumpIfTargetInLOS("Missile")
		GUNT A 0 A_ClearTarget
		goto Idle
	Clear:
		GUNT A 2 A_ClearTarget
		goto See
	Missile:
		GUNT A 0 A_JumpIfInTargetInventory ("IsBlue", 1, 1)
		goto Clear
		GUNT A 0 A_JumpIfInTargetInventory("StealthMode", 1, "Clear")
		GUNT A 0 A_JumpIfCloser(1280,"MissileAuto")
		GUNT A 0 A_JumpIfCloser(3072,"MissileBurst")
		goto Clear
	MissileAuto:
		GUNT B 4 A_FaceTarget //Turret shot delay
		GUNT B 0 A_FaceTarget
		GUNT B 0 A_SpawnItemEx("ShellCasing", 8, 8, 48, random(1,3), random(0,1), random(1,3), random(45,60), 128)
		GUNT C 2 BRIGHT A_CustomBulletAttack (random(15,33)*0.14, random(15,33)*0.13, 1, 10, "HandgunPuff", 3072, CBAF_NORANDOM)
		GUNT B 2 
		GUNT A 0 A_JumpIfCloser(1280, 2) // Check if player is close to switch shoting mode.
		GUNT A 0 A_CPosRefire
		goto MissileBurst+1
		GUNT A 0 A_CPosRefire
		goto MissileAuto+1
	MissileBurst:
		GUNT B 4 A_FaceTarget // Turret shot delay
		GUNT B 0 A_FaceTarget
		GUNT B 0 A_SpawnItemEx("ShellCasing",8,8,48,random(1,3),random(0,1),random(1,3),random(45,60),128)
		GUNT C 2 BRIGHT A_CustomBulletAttack (1.5, 1.5, 1, 10, "HandgunPuff", 3072, CBAF_NORANDOM)
		GUNT B 2 
		GUNT B 0 A_FaceTarget
		GUNT B 0 A_SpawnItemEx("ShellCasing",8,8,48,random(1,3),random(0,1),random(1,3),random(45,60),128)
		GUNT C 2 BRIGHT A_CustomBulletAttack (1.5, 1.5, 1, 10, "HandgunPuff", 3072, CBAF_NORANDOM)
		GUNT B 2 
		GUNT B 0 A_FaceTarget
		GUNT B 0 A_SpawnItemEx("ShellCasing",8,8,48,random(1,3),random(0,1),random(1,3),random(45,60),128)
		GUNT C 2 BRIGHT A_CustomBulletAttack (1.5, 1.5, 1, 10, "HandgunPuff", 3072, CBAF_NORANDOM)
		GUNT B 2
		GUNT A 7 // Sub-machinegun delay on burst shot
		GUNT A 0 A_JumpIfCloser(1280, 2) // Check if player is close to switch shoting mode.
		GUNT A 0 A_CPosRefire
		goto MissileBurst+1
		GUNT A 0 A_JumpIfCloser(3072, 1) // Check if player is on max range to still shot.
		goto Clear
		GUNT A 0 A_CPosRefire
		goto MissileAuto+1
	Pain:
		GUNT A 2 A_ClearTarget
		goto See
	}
}

// Use Red turret code to decrease code usage.
actor Utility_GunTurretBlue : Utility_GunTurretRed {
	obituary "%o was owned by a \chBlue \c-turret."
	DesignatedTeam 0
	states {

	Spawn:
		GUNT A 0
		GUNT A 0 Thing_SetSpecial(0, 84, 345, 0, TEAM_BLUE)
	See:
		GUNT A 0 A_GiveInventory("IsBlue", 1)
		GUNT A 0 A_ChangeFlag("FRIENDLY", 0)
		GUNT A 2 A_JumpIfTargetInLOS("Missile")
		GUNT A 0 A_ClearTarget
		goto Idle
	Missile:
		GUNT A 1 A_JumpIfInTargetInventory ("IsRed", 1, 1)
		goto Clear
		GUNT A 0 A_JumpIfInTargetInventory("StealthMode", 1, "Clear")
		GUNT A 0 A_JumpIfCloser(1280,"MissileAuto")
		GUNT A 0 A_JumpIfCloser(3072,"MissileBurst")
		goto Clear
	}
}

actor GunTurretClip : Inventory {inventory.maxamount 23}