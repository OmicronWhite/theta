//Flash Grenades
actor FlashGrenade : AOWWeapon {
    +NOEXTREMEDEATH
    +NOALERT
    inventory.pickupmessage "Picked up a Flash Grenade."
    Weapon.kickback 50
    Weapon.AmmoType "FlashGrenadeAmmo"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 1
	Weapon.AmmoType2 "FlashGrenadePower"
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
    Weapon.UpSound "weapons/select00"
	Weapon.PreferredSkin "Fist_Marine"
	DropItem "FlashGrenadeAmmo"
    Tag "Flash Grenade"

    States {
    Ready:
		FLSH A 0
		FLSH A 0 A_JumpIfNoAmmo ("ReadyNoAmmo")
	ReadyIdle:
        TNT1 A 0 A_JumpIfInventory("ThrowFragGrenade",1,"FragGrenade")
        TNT1 A 0 A_TakeInventory("ThrowFlashGrenade",1)
		TNT1 A 0 A_JumpIfInventory("UseRemoteC4Detonator",1,"RemoteC4Detonator")
		FLSH A 1 A_WeaponReady
		loop
	ReadyNoAmmo:
        TNT1 A 0 A_TakeInventory("ThrowFlashGrenade",1)
        TNT1 A 0 A_JumpIfInventory("FlashGrenadeAmmo",1,"Selected")
		TNT1 A 0 A_JumpIfInventory("ThrowFragGrenade",1,"FragGrenade")
		TNT1 A 0 A_JumpIfInventory("UseRemoteC4Detonator",1,"RemoteC4Detonator")
		TNT1 A 1 A_WeaponReady
		loop
    Deselect:
	    FLSH A 0 A_JumpIfNoAmmo ("DeselectNoAmmo")
		FLSH A 0 A_Lower
		FLSH A 1 A_Lower
		goto Deselect+1
	DeselectNoAmmo:
    	TNT1 A 0 A_Lower
	    TNT1 A 1 A_Lower
	    loop
    Select:
	    FLSH A 0 A_JumpIfNoAmmo ("SelectNoAmmo")
		FLSH A 0 A_Raise
		FLSH A 1 A_Raise
		goto Select+1
	SelectNoAmmo:
		 // [K6] Workaround for suicide bomber crash - don't try to raise a nonexistent frag/flash grenade.
		TNT1 A 0 A_SelectWeapon ("Bomb Pack")
		TNT1 A 0 A_Raise
		TNT1 A 1 A_Raise
		loop
    Selected:
	    FLSH A 0 A_JumpIfNoAmmo ("SelectNoAmmo")
		FLSH A 0 A_PlayWeaponSound ("weapons/select00")
		FLSH A 1 offset (120,139)
		FLSH A 1 offset (100,112)
		FLSH A 1 offset (80,89)
		FLSH A 1 offset (60,70)
		FLSH A 1 offset (40,55)
		FLSH A 1 offset (20,44)
		FLSH A 1 offset (10,37)
		FLSH A 1 offset (0,0)
		Goto Ready
    Fire:
        TNT1 A 0 A_JumpIfInventory("IsInSpawnRoom",1,"Ready")
        TNT1 A 0 A_JumpIfInventory ("InMenu", 1, "Ready")
		TNT1 A 0 A_TakeInventory ("FlashGrenadePower", 100)
        FLSH BCDEFGHIJKLMNOPQR 1
		FLSH S 1 A_ReFire
		FSH1 A 1
        FSH1 A 0 A_PlaySound("flash/flashthrow", CHAN_WEAPON)
		FSH1 A 0 A_FireCustomMissile("FlashGrenadeThrown",0,1,0,0)
        FSH1 A 0 A_TakeInventory ("FlashGrenadePower", 100)
        FSH1 BCD 1
        FSH1 E 1 A_CheckReload
        Goto Select
	Hold:
		TNT1 A 0 A_JumpIfInventory ("IsInSpawnRoom", 1, "Select")
		TNT1 A 0 A_JumpIfInventory ("InMenu", 1, "Select")
		TNT1 A 2 A_GiveInventory ("FlashGrenadePower", 3)
		TNT1 A 1 A_ReFire
		TNT1 A 3
		TNT1 A 0 A_JumpIfInventory ("FlashGrenadePower", 100, "Throw10")
		TNT1 A 0 A_JumpIfInventory ("FlashGrenadePower", 90, "Throw9")
		TNT1 A 0 A_JumpIfInventory ("FlashGrenadePower", 80, "Throw8")
		TNT1 A 0 A_JumpIfInventory ("FlashGrenadePower", 70, "Throw7")
		TNT1 A 0 A_JumpIfInventory ("FlashGrenadePower", 60, "Throw6")
		TNT1 A 0 A_JumpIfInventory ("FlashGrenadePower", 50, "Throw5")
		TNT1 A 0 A_JumpIfInventory ("FlashGrenadePower", 40, "Throw4")
		TNT1 A 0 A_JumpIfInventory ("FlashGrenadePower", 30, "Throw3")
		TNT1 A 0 A_JumpIfInventory ("FlashGrenadePower", 20, "Throw2")
        FSH1 A 1
		FLSH D 0 A_PlaySound ("flash/flashthrow", CHAN_WEAPON)
		FLSH D 0 A_FireCustomMissile ("FlashGrenadeThrown", -1, 1, 10, 5)
	FireEnd:
		TNT1 A 0 A_TakeInventory ("FlashGrenadePower", 100)
        FSH1 BCD 1
        FSH1 E 1 A_CheckReload
        Goto Select
	FireEnd2:
		TNT1 A 0 A_TakeInventory ("FlashGrenadePower", 100)
        FSH2 BCDEFGH 1
        FSH2 I 1 A_CheckReload
		goto Select
	Throw2:
        FSH1 A 1
		FSH1 A 0 A_PlaySound ("flash/flashthrow", CHAN_WEAPON)
		FSH1 A 0 A_FireCustomMissile ("FlashGrenadeThrown2", -1, 1, 10, 5)
		goto FireEnd
	Throw3:
        FSH1 A 1
		FSH1 A 0 A_PlaySound ("flash/flashthrow", CHAN_WEAPON)
		FSH1 A 0 A_FireCustomMissile ("FlashGrenadeThrown3", -1, 1, 10, 5)
		goto FireEnd
	Throw4:
        FSH2 A 1
		FSH2 A 0 A_PlaySound ("flash/flashthrow", CHAN_WEAPON)
		FSH2 A 0 A_FireCustomMissile ("FlashGrenadeThrown4", -1, 1, 10, 15)
		goto FireEnd2
	Throw5:
	    FSH2 A 1
		FSH2 A 0 A_PlaySound ("flash/flashthrow", CHAN_WEAPON)
		FSH2 A 0 A_FireCustomMissile ("FlashGrenadeThrown5", -1, 1, 10, 15)
		goto FireEnd2
	Throw6:
		FSH2 A 1
		FSH2 A 0 A_PlaySound ("flash/flashthrow", CHAN_WEAPON)
		FSH2 A 0 A_FireCustomMissile ("FlashGrenadeThrown6", -1, 1, 10, 15)
		goto FireEnd2
	Throw7:
	    FSH2 A 1
		FSH2 A 0 A_PlaySound ("flash/flashthrow", CHAN_WEAPON)
		FSH2 A 0 A_FireCustomMissile ("FlashGrenadeThrown7", -1, 1, 10, 15)
		goto FireEnd2
	Throw8:
	    FSH2 A 1
		FSH2 A 0 A_PlaySound ("flash/flashthrow", CHAN_WEAPON)
		FSH2 A 0 A_FireCustomMissile ("FlashGrenadeThrown8", -1, 1, 10, 15)
		goto FireEnd2
	Throw9:
	    FSH2 A 1
		FSH2 A 0 A_PlaySound ("flash/flashthrow", CHAN_WEAPON)
		FSH2 A 0 A_FireCustomMissile ("FlashGrenadeThrown9", -1, 1, 10, 15)
		goto FireEnd2
	Throw10:
	    FSH2 A 1
		FSH2 A 0 A_PlaySound ("flash/flashthrow", CHAN_WEAPON)
		FSH2 A 0 A_FireCustomMissile ("FlashGrenadeThrown10", -1, 1, 10, 15)
		goto FireEnd2
    FragGrenade:
	    TNT1 A 0 A_JumpIfNoAmmo ("FragGrenadeNoAmmo")
		FLSH A 1 offset (4,34)
		FLSH A 1 offset (16,49)
		FLSH A 1 offset (32,79)
		FLSH A 1 offset (48,125)
        TNT1 A 0 offset (1,33)
	FragGrenadeNoAmmo:
	    TNT1 A 1
	    goto FragGrenade2
	FragGrenade2:
		FRAG BCD 1
		FRAG EFG 2
		FRAG H 2 A_PlaySound ("frag/pinpull", CHAN_WEAPON)
		FRAG IJ 2
		FRAG KLMNOPQR 1
		TNT1 A 10
	    FSH2 A 1
		FSH2 A 0 A_PlaySound ("frag/throw", CHAN_WEAPON)
		FSH2 A 0 A_FireCustomMissile ("FragGrenadeThrown5", -1, 0, 10, 15)
		TNT1 A 0 A_TakeInventory("FragGrenadeAmmo",1)
        TNT1 A 0 A_TakeInventory("ThrowFragGrenade",1)
        TNT1 A 0 A_TakeInventory("ThrowFlashGrenade",1)
		TNT1 A 0 A_TakeInventory("UseRemoteC4Detonator",1)
        FSH2 BCDEFGHI 1
		goto Selected
    RemoteC4Detonator:
	    TNT1 A 0 A_JumpIfNoAmmo ("RemoteC4DetonatorNoAmmo")
		FLSH A 1 offset (4,34)
		FLSH A 1 offset (16,49)
		FLSH A 1 offset (32,79)
		FLSH A 1 offset (48,125)
		TNT1 A 0 offset (1,33)
		goto RemoteC4Detonator2
	RemoteC4DetonatorNoAmmo:
	    TNT1 A 1
	    goto RemoteC4Detonator2
	RemoteC4Detonator2:
		RC4K ABCDEFGH 1
		RC4K I 2
		RC4K J 2 A_PlaySound ("c4/remote2", CHAN_WEAPON)
		RC4K I 4 A_GiveInventory ("C4DetonateCheck", 1)
		RC4K H 6
		RC4K G 1 A_TakeInventory ("C4DetonateCheck", 666)
        TNT1 A 0 A_TakeInventory("ThrowFragGrenade",1)
        TNT1 A 0 A_TakeInventory("ThrowFlashGrenade",1)
        TNT1 A 0 A_TakeInventory("UseRemoteC4Detonator",1)
		RC4K FEDCBA 1
		goto Selected
	Spawn:
		TNT1 A 0
		stop
   }
}

actor FlashGrenadePower : Ammo {
	+IGNORESKILL
	inventory.amount 0
	inventory.maxamount 100
	ammo.backpackamount 0
	ammo.backpackmaxamount 100
}

actor Flashed : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}

//Check this to use Flashbang Grenades
actor ThrowFlashGrenade : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}

// =============================================================================
actor FlashGrenadeThrown {
    Obituary "$OB_FLASHGRENADE"
    Radius 5
    Height 5
    Speed 17
    Damage (0)
    PROJECTILE
    -NOGRAVITY
    +HEXENBOUNCE
    +NOEXTREMEDEATH
    +FORCERADIUSDMG
    +CANBOUNCEWATER
    +BOUNCEONACTORS
    +ROCKETTRAIL
    +FORCEXYBILLBOARD
    Scale 0.22
    Gravity 0.4
    Bouncefactor 0.4
    Wallbouncefactor 0.5
	ReactionTime 2
    Seesound "flash/flashbounce"
    DamageType "Flash"
    States {
    Spawn:
        FBGT ABCDEFG 3
        FBGT H 3 A_Countdown
        loop
    Death:
 	    TNT1 A 0 A_PlaySound ("flash/flash")
        TNT1 A 0 A_Explode(20,512)
        TNT1 A 0 A_SpawnItemEx("FlashbangFlare", 0, 0, 0, 0, 0, 0, 0, 128, 0)
        TNT1 A 5 A_SpawnItemEx("FlashbangFlare", 0, 0, 0, 0, 0, 0, 0, 128, 0)
        stop
  }
}

actor FlashbangFlare
{
  RenderStyle Add
  alpha 0.9
  -RANDOMIZE
  +NOCLIP
  +NOGRAVITY
  Scale 5.0
  states
  {
    Spawn:
        PPCX AAAAAAAAA 2 bright A_FadeOut(0.15)
        stop
  }
}

// =============================================================================
actor FlashGrenadeDropped : FlashGrenadeThrown {Speed 10}
actor FlashGrenadeThrown2 : FlashGrenadeThrown {Speed 21}
actor FlashGrenadeThrown3 : FlashGrenadeThrown {Speed 24}
actor FlashGrenadeThrown4 : FlashGrenadeThrown {Speed 27}
actor FlashGrenadeThrown5 : FlashGrenadeThrown {Speed 30}
actor FlashGrenadeThrown6 : FlashGrenadeThrown {Speed 33}
actor FlashGrenadeThrown7 : FlashGrenadeThrown {Speed 36}
actor FlashGrenadeThrown8 : FlashGrenadeThrown {Speed 39}
actor FlashGrenadeThrown9 : FlashGrenadeThrown {Speed 42}
actor FlashGrenadeThrown10 : FlashGrenadeThrown {Speed 45}
