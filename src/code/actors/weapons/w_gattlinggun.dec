// Gatling Gun
// Bullet - Close combat - No dependency - Price:400

actor GatlingGun : AOWWeapon {
	obituary "$OB_GATLING" // "%o was perforated by %k's gatling gun."
	inventory.pickupmessage "You got the gatling gun!"
	weapon.selectionorder 7
	weapon.ammotype "ChainAmmo"
	weapon.ammouse 1
	weapon.ammogive 120
	weapon.ammotype2 "ChainAmmo"
	weapon.ammouse2 2
	weapon.preferredskin "MarineChaingun"

	States {
	Ready:
		GATG A 0 A_StopSound (CHAN_WEAPON)
		GATG A 5 A_WeaponReady
		loop

	Deselect:
		GATG A 0 A_StopSound (CHAN_WEAPON)
		GATG AAA 1 A_Lower
		loop

	Select:
		GATG A 1 A_Raise
		loop

	Fire:
		GATG B 5 A_PlaySound ("gatling/spinup", 5)
		GATG A 4
		GATG B 3
	Hold:
		GATG A 1 A_PlaySound ("minigun/loop", CHAN_WEAPON, 1.0, 1)
		GATG A 0 A_Light2
		GATG A 0 A_PlaySound ("minigun/fire2", 5)
		GATG A 0 A_SpawnItemEx("RifleShellCasing",2,8,32,random(1,3),random(0,1),random(1,3),random(45,60),128)
		GATF A 1 BRIGHT A_FireBullets (7.2, 7.2, -1, 13, "RiflePuff", FBF_USEAMMO|FBF_NORANDOM)
		GATG B 1 A_Light1
		GATG B 0 A_Light2
		GATF B 0 A_SpawnItemEx("RifleShellCasing",2,8,32,random(1,3),random(0,1),random(1,3),random(45,60),128)
		GATF B 1 BRIGHT A_FireBullets (7.2, 7.2, -1, 13, "RiflePuff", FBF_USEAMMO|FBF_NORANDOM)
		GATG A 1 A_Light1
		GATG A 0 A_ReFire
		GATG B 0 A_Light0
		GATG B 3 A_PlaySound ("gatling/spin", 5)
		GATG A 0 A_StopSound (CHAN_WEAPON)
		GATG A 5 A_ReFire
		GATG B 6 A_ReFire
		GATG AB 7
		goto Ready

	AltFire:
		GATG B 5 A_PlaySound ("gatling/spinup", 5)
		GATG A 4
		GATG B 3
	AltHold:
		GATF A 0 BRIGHT A_PlaySound ("gatling/alt", CHAN_WEAPON, 1, 1.0)
		GATF A 0 BRIGHT A_Light2
		GATF AA 0 BRIGHT A_SpawnItemEx("RifleShellCasing",2,8,32,random(1,3),random(0,1),random(1,3),random(45,60),128)
		GATF A 2 BRIGHT A_FireBullets (2.6, 1.6, -1, 20, "ExplosionPuffSmall", FBF_USEAMMO|FBF_NORANDOM, 3072)
		GATG B 3 A_Light1
		GATF B 0 A_Light2
		GATF BB 0 BRIGHT A_SpawnItemEx("RifleShellCasing",2,8,32,random(1,3),random(0,1),random(1,3),random(45,60),128)
		GATF B 2 BRIGHT A_FireBullets (2.6, 1.6, -1, 20, "ExplosionPuffSmall", FBF_USEAMMO|FBF_NORANDOM, 3072)
		GATG A 3 A_Light1
		GATG A 0 A_ReFire
		GATG B 0 A_Light0
		GATG B 3 A_PlaySound ("gatling/spin", 5)
		GATG A 0 A_StopSound (CHAN_WEAPON)
		GATG A 5 A_ReFire
		GATG B 6 A_ReFire
		GATG AB 7
		goto Ready

	Spawn:
		GATG X -1
		stop
	}
}

actor ExplosionPuff {
	+PUFFONACTORS
	+CLIENTSIDEONLY
	DamageType "Explosion"

	States {
	Spawn:
		TNT1 A 0
		TNT1 A 14 A_SpawnItemEx ("ExplosionSmall", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION)
		stop
	}
}

actor ExplosionPuffSmall : ExplosionPuff {
	States {
	Spawn:
		TNT1 A 0
		TNT1 A 14 A_SpawnItemEx ("ExplosionSmaller", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION)
		stop
	}
}

// Map Actor

actor GatlingGunPickup : CustomInventory 21045 {
	//$Category Theta Weapons
	inventory.pickupmessage "You got the gatling gun!"
	inventory.maxamount 0
	inventory.respawntics 2100
	inventory.pickupsound "misc/w_pkup"

	states {
	Spawn:
		GATG X -1
		stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory ("GatlingGun", 1, "Failure")
		TNT1 A 0 A_GiveInventory ("GatlingGun", 1)
		TNT1 A 0 A_GiveInventory ("ChainAmmo", 50)
		TNT1 A 0 A_GiveInventory ("Refunds", 133)
		stop
	Failure:
		TNT1 A 0
		fail
	}
}
