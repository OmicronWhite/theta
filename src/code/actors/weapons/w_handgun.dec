// Handgun
// Bullet - Generic - No dependency - Price: Free

actor Handgun : AOWWeapon {
	+WIMPY_WEAPON
	+NOAUTOFIRE
	Weapon.AmmoUse 1
	Weapon.AmmoGive 12
	Weapon.AmmoType "HandgunAmmo"
	Weapon.SelectionOrder 19
	Weapon.PreferredSkin "MarinePistol"
	Inventory.Pickupmessage "You got the handgun!"
	Obituary "%o was capped by %k's handgun."
	attacksound "pistol/fire"
	dropitem "HandgunPickup"

	States {
	Ready:
		PISG A 1 A_WeaponReady
		Loop

	Deselect:
		PISG A 0 A_Lower
		PISG A 1 A_Lower
		Loop

	Select:
		PISG A 0 A_Raise
		PISG A 1 A_Raise
		Loop

	Fire:
		PISG B 0 A_SpawnItemEx("ShellCasing",8,8,32,random(1,3),random(0,1),random(1,3),random(45,60),128)
		PISG B 0 A_FireBullets (2.0, 2.0, -1, 8, "HandgunPuff", FBF_USEAMMO|FBF_NORANDOM, 1536)
		PISG B 4 A_GunFlash
		PISG CC 1 A_WeaponReady
		PISG BBB 1 A_WeaponReady
		PISG A 0 A_ReFire
		goto Ready

	Flash:
		PISF A 1 Bright A_Light2
		PISF A 1 Bright A_Light1
		Goto LightDone

	Spawn:
		PIST A -1
		stop
	}
}

actor HandgunPuff : HHBulletPuff {
	DamageType "Bullet"
	+FORCEXYBILLBOARD
}

// Map Actor

actor HandgunPickup : CustomInventory replaces Pistol 21041 {
	//$Category Theta Weapons
	inventory.pickupmessage "You got the handgun!"
	inventory.maxamount 0
	inventory.respawntics 2100
	inventory.pickupsound "misc/w_pkup"
	states {

	Spawn:
		PIST A -1
		stop
	Pickup:
		TNT1 A 0 A_GiveInventory("Handgun", 1)
		TNT1 A 0 A_GiveInventory("HandgunAmmo", 12)
		stop
	Failure:
		TNT1 A 0
		fail
	}
}